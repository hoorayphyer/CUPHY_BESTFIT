Function CUPHYS_BESTFIT(xdata As Range, xerror As Range, ydata As Range, yerror As Range) As Variant

'PART ONE : Minimize Chi Squared to Find Slope and Intercept
    Dim x2s2 As Double
    Dim xs2 As Double
    Dim ys2 As Double
    Dim xys2 As Double
    Dim invs2 As Double
    x2s2 = 0
    xs2 = 0
    ys2 = 0
    xys2 = 0
    invs2 = 0

    Dim x As Double
    Dim y As Double
    Dim s2 As Double
    For i = 1 To xdata.Rows.Count
        x = xdata.Cells(i, 1)
        y = ydata.Cells(i, 1)
        s2 = yerror.Cells(i, 1) * yerror.Cells(i, 1)

        x2s2 = x2s2 + x * x / s2
        xs2 = xs2 + x / s2
        ys2 = ys2 + y / s2
        xys2 = xys2 + x * y / s2
        invs2 = invs2 + 1 / s2
    Next i

    Dim normalization As Double
    Dim bf_slope As Double
    Dim bf_error_slope As Double
    Dim bf_intercept As Double
    Dim bf_error_intercept As Double

    ' normalization equal to zero will cause #VALUE error
    normalization = invs2 * x2s2 - xs2 * xs2
    bf_slope = (invs2 * xys2 - xs2 * ys2) / normalization
    bf_error_slope = VBA.Sqr(invs2 / normalization) 'note VBA.Sqr
    bf_intercept = (x2s2 * ys2 - xs2 * xys2) / normalization
    bf_error_intercept = VBA.Sqr(x2s2 / normalization)

    'results stored in a 2x4 array
    Dim results(1 To 2, 1 To 4) As Variant
    results(1, 1) = "slope"
    results(2, 1) = bf_slope
    results(1, 2) = "error in slope"
    results(2, 2) = bf_error_slope
    results(1, 3) = "intercept"
    results(2, 3) = bf_intercept
    results(1, 4) = "error in intercept"
    results(2, 4) = bf_error_intercept

    'Array formulas must be entered with Ctrl + Shift + Enter rather than just the Enter key.
    CUPHYS_BESTFIT = results

'PART TWO : Plot

End Function
